`timescale 1 ns / 1 ns

module matmul_tb ();

    // files generated by the C code
    localparam string A_NAME = "x.txt";
    localparam string B_NAME = "y.txt"; 
    localparam string C_NAME = "z.txt"; 
    
    localparam DATA_WIDTH = 32;
    localparam ADDR_WIDTH = 10;
    localparam N = 8;                   
    localparam NUM_ELEMENTS = 64;       
    localparam CLOCK_PERIOD = 10;   

    logic clock = 1'b0;
    logic reset = 1'b0;
    logic start = 1'b0;
    logic done;

    logic [DATA_WIDTH-1:0] a_din;
    logic [ADDR_WIDTH-1:0] a_wr_addr;
    logic a_wr_en;
    logic [DATA_WIDTH-1:0] b_din;
    logic [ADDR_WIDTH-1:0] b_wr_addr;
    logic b_wr_en;
    logic [DATA_WIDTH-1:0] c_dout;
    logic [ADDR_WIDTH-1:0] c_rd_addr;

    logic   a_write_done = '0;
    logic   b_write_done = '0;
    logic   c_read_done  = '0;
    integer c_errors     = '0;

    matmul_top #(
        .DATA_WIDTH(DATA_WIDTH),
        .ADDR_WIDTH(ADDR_WIDTH),
        .N(N)
    ) matmul_top_inst (
        .clock(clock),
        .reset(reset),
        .start(start),
        .done(done),
        .a_wr_addr(a_wr_addr),
        .a_wr_en(a_wr_en),
        .a_din(a_din),
        .b_wr_addr(b_wr_addr),
        .b_wr_en(b_wr_en),
        .b_din(b_din),
        .c_rd_addr(c_rd_addr),
        .c_dout(c_dout)
    );

    // clock process
    always begin
        #(CLOCK_PERIOD/2) clock = 1'b1;
        #(CLOCK_PERIOD/2) clock = 1'b0;
    end

    // reset process
    initial begin
        #(CLOCK_PERIOD) reset = 1'b1;
        #(CLOCK_PERIOD) reset = 1'b0;
    end

    initial begin
        time start_time, end_time;

        @(negedge reset);
        
        wait(a_write_done && b_write_done);
        
        @(posedge clock);
        start_time = $time;
        $display("@ %0t: Beginning simulation...", start_time);

        #(CLOCK_PERIOD) start = 1'b1;
        #(CLOCK_PERIOD) start = 1'b0;
        
        wait(done);

        end_time = $time;
        $display("@ %0t: Simulation completed.", end_time);
        
        wait(c_read_done);
        
        $display("Total simulation cycle count: %0d", (end_time-start_time)/CLOCK_PERIOD);
        $display("Total error count: %0d", c_errors);

        $stop;
    end

    initial begin : a_write
        integer fd, i, count;
        a_wr_addr = '0;
        a_wr_en = 1'b0;

        @(negedge reset);
        $display("@ %0t: Loading file %s...", $time, A_NAME);
        
        fd = $fopen(A_NAME, "r");
        if (fd == 0) begin
            $display("Error: Could not open %s", A_NAME);
            $stop;
        end

        for (i = 0; i < NUM_ELEMENTS; i++) begin
            @(posedge clock);
            count = $fscanf(fd, "%h", a_din);
            a_wr_addr = i;
            a_wr_en = 1'b1;
        end

        @(posedge clock);
        a_wr_en = 1'b0;
        $fclose(fd);
        a_write_done = 1'b1;
    end

    initial begin : b_write
        integer fd, i, count;
        b_wr_addr = '0;
        b_wr_en = 1'b0;
        
        @(negedge reset);
        $display("@ %0t: Loading file %s...", $time, B_NAME);
        
        fd = $fopen(B_NAME, "r");
        if (fd == 0) begin
            $display("Error: Could not open %s", B_NAME);
            $stop;
        end

        for (i = 0; i < NUM_ELEMENTS; i++) begin
            @(posedge clock);
            count = $fscanf(fd, "%h", b_din);
            b_wr_addr = i;
            b_wr_en = 1'b1;
        end

        @(posedge clock);
        b_wr_en = 1'b0;
        $fclose(fd);
        b_write_done = 1'b1;
    end

    initial begin : c_verify
        integer fd, i, count;
        logic [DATA_WIDTH-1:0] c_data_cmp, c_data_read;
        c_rd_addr = '0;

        @(negedge reset);
        wait(done);
        @(negedge clock); 

        $display("@ %0t: Comparing file %s...", $time, C_NAME);
        
        fd = $fopen(C_NAME, "r");
        if (fd == 0) begin
            $display("Error: Could not open %s", C_NAME);
            $stop;
        end

        for (i = 0; i < NUM_ELEMENTS; i++) begin
            @(negedge clock);
            c_rd_addr = i;
            
            @(negedge clock);
            
            count = $fscanf(fd, "%h", c_data_cmp);
            c_data_read = c_dout;
            
            if (c_data_read !== c_data_cmp) begin
                c_errors++;
                $display("@ %0t: %s(%0d): ERROR: Hardware %h != Reference %h at address 0x%h.", 
                         $time, C_NAME, i, c_data_read, c_data_cmp, i);
            end
            @(posedge clock);
        end
        $fclose(fd);
        c_read_done = 1'b1;
    end

endmodule